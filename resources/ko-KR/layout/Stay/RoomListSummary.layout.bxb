layout-macro-def (room-list-summary) {
  params {
    param (room) {
      type (Room)
      min (Required) max (Many)
    }
  }
  content {    
    thumbnail-card {
      image-position (Start)
      image-url ("#{value(room.roomUrlImage)}")
      title-area {
        halign (Start)
        slot1 {
          text {
            value ("#{value(room.roomName)}")
            style (Title_S)
          }
        }
        slot2 {
          single-line {
            if (exists(room.roomStandardPersonnel) && exists(room.roomMaximumPersonnel)) {
              text{
                value ("기준 #{value(room.roomStandardPersonnel)}명 / 최대 #{value(room.roomMaximumPersonnel)}명")
                style (Detail_M_Soft)
              }
            }
          }
        }
        slot3 {
          paragraph {
            value {        
              if (exists(room.roomRentalAvailable) && exists(room.roomHoursPrice)) {
                // 방의 대실 이용시간(roomHoursAvailable, int type)이 0보다 크고, 방의 대실 이용가능여부(roomRentalAvailable, boolean type) 데이터가 있는 경우
                template ("대실 #{value(room.roomHoursPrice)}원 / 숙박 #{value(room.roomDaysPrice)}원")
              } else-if (exists(room.roomHoursAvailable) && !exists(room.roomRentalAvailable)) {
                // 방의 대실 이용시간(roomHoursAvailable, int type)이 0보다 크나, 방의 대실 이용가능여부(roomResntalAvailable, boolean type)가 false인 경우
                template ("대실 예약 마감 / 숙박 #{value(room.roomDaysPrice)}원")
              } else { 
                // 방의 대실 이용시간(room.roomHoursAvailable, int type)이 존재하지 않는 경우
                template ("숙박 #{value(room.roomDaysPrice)}원")
              }
            }
            style (Detail_L)
          }
        }
      }
      on-click {
        intent {
          goal : SelectRoom
          value : $expr (room.roomId)
          value : $expr (room.dateInterval)
        }
      }
    }
  }
}